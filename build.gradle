apply plugin: 'java'
apply plugin: 'eclipse'

sourceCompatibility = 1.7

sourceSets {
    main {
        java {
            srcDir 'src/'
            srcDir 'gen/'
        }
        resources {
            srcDir 'res/'
        }
    }
}

buildDir = 'out/'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.eclipse.birt.runtime.3_7_1:com.lowagie.text:2.1.7'
    compile 'javax.mail:javax.mail-api:1.5.4'
    compile 'org.apache.poi:poi:3.13'
    compile 'net.java.dev.glazedlists:glazedlists_java15:1.9.1'
    compile 'org.jooq:jooq:3.6.0'

    runtime 'mysql:mysql-connector-java:5.1.16'
}

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        classpath 'org.jooq:jooq-codegen:3.6.0'
        classpath 'mysql:mysql-connector-java:5.1.16'
    }
}

// Use your favourite XML builder to construct the code generation configuration file
// ----------------------------------------------------------------------------------
def writer = new StringWriter()

Properties dbSettings = new Properties()
dbSettings.load(new FileInputStream("res/sql_auth.properties"))

def xml = new groovy.xml.MarkupBuilder(writer)
.configuration('xmlns': 'http://www.jooq.org/xsd/jooq-codegen-3.6.0.xsd') {
    jdbc() {
        driver('com.mysql.jdbc.Driver')
        url('jdbc:mysql://' + dbSettings['server'] + '/')
        user(dbSettings['username'])
        password(dbSettings['password'])
    }
    generator() {
        database() {
            // this is a terrible hack, but unfortunately necessary because JOOQ doesn't
            // detect primary keys in views
            syntheticPrimaryKeys('fullbanf.id')
        }
        generate() {
        }
        target() {
            packageName('sql.generated')
            directory('gen/')
        }
        pojos() {
            true
        }
    }
}

// Run the code generator
// ----------------------
org.jooq.util.GenerationTool.generate(
    javax.xml.bind.JAXB.unmarshal(new StringReader(writer.toString()), org.jooq.util.jaxb.Configuration.class)
)
